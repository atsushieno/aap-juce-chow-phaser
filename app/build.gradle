plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

kotlin {
    jvmToolchain(17)
}

android {
    namespace "org.androidaudioplugin.ports.juce.chow_phaser"
    compileSdk = libs.versions.android.compileSdk.get().toInteger()
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.juce.chow_phaser"
        minSdk = libs.versions.android.minSdk.get().toInteger()
        ndk {
            // should we make it customizable? We skip others just to reduce build time.
            abiFilters "x86_64", "arm64-v8a"
        }
        externalNativeBuild {
            cmake {
                arguments "-DAAP_DIR=${System.env.AAP_DIR != null ? System.env.AAP_DIR : "$projectDir/../external/aap-core"}",
                        "-DAAP_JUCE_DIR=${System.env.AAP_JUCE_DIR != null ? System.env.AAP_JUCE_DIR : "$projectDir/../external/aap-juce"}",
                        "-DANDROID_STL=c++_shared", "-DANDROID_CPP_FEATURES=exceptions rtti"//, "-DANDROID_ARM_NEON=ON"
            }
        }
    }

    buildFeatures {
        prefab true
    }

    sourceSets {
        main.java.srcDirs +=
            ["../external/ChowPhaser/modules/JUCE/modules/juce_gui_basics/native/javaopt/app",
             "../external/aap-juce/java"]
        main.res.srcDirs +=
            []
    }

    dependencies {
        implementation libs.androidx.core.ktx
        implementation libs.startup.runtime
        implementation libs.aap.core
        implementation libs.aap.ui.web
        implementation libs.aap.ui.compose
    }

    ndkVersion libs.versions.ndk.get()
}

